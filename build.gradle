allprojects {
  ext.kotlinVersion = '1.2.41'

  ext.javaxInjectVersion = '1'
  ext.jsr305Version = '3.0.2'
  ext.jcommanderVersion = '1.72'
  ext.asmVersion = '5.2'
  ext.gripVersion = '0.5.3-beta'
  ext.bimapVersion = '1.0.2'
  ext.logbackVersion = '1.2.3'

  ext.junitVersion = '4.12'
  ext.mockitoVersion = '2.17.0'
  ext.espressoVersion = '3.0.1'
  ext.testRunnerVersion = '1.0.1'

  ext.androidCompileSdkVersion = 27
  ext.androidTargetSdkVersion = 27
  ext.androidMinSdkVersion = 16
  ext.androidBuildToolsVersion = '27.0.3'
  ext.androidToolsVersion = '3.1.3'
  ext.supportVersion = '27.1.1'

  buildscript {
    repositories {
      mavenLocal()
      jcenter()
      google()
    }

    dependencies {
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
      classpath "com.android.tools.build:gradle:$androidToolsVersion"
    }
  }

  repositories {
    mavenLocal()
    jcenter()
    google()
  }
}

allprojects {
  def properties = new Properties()
  properties.load(new FileInputStream(new File(rootDir, "version.properties")))
  def major = properties['version.major'] as int
  def minor = properties['version.minor'] as int
  def patch = properties['version.patch'] as int
  def suffix = properties['version.suffix']?.toString()?.trim() ?: ""
  def snapshot = properties['version.snapshot'].toBoolean()
  version = "$major.$minor.$patch" + (suffix.isAllWhitespace() ? '' : "-$suffix") + (snapshot ? '-SNAPSHOT' : '')
  group = 'io.michaelrocks'

  ext.dryRun = properties['version.dryRun'].toBoolean()
  ext.publish = properties['version.publish'].toBoolean()
}
